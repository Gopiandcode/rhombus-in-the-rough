#lang rhombus

import:
  lib("raylib/2d/unsafe.rkt") as raylib

  "../lib/datatypes.rhm" open
  "../lib/ecs.rhm" open
  "../lib/lang.rhm" open
  "../lib/resources.rhm" expose: def_texture
  "../game/raylib_lang.rhm" open
  "../lib/adt.rhm"
  "../lib/spritesheet.rhm" expose: Sprite

  "../lib/event.rhm"
  "../messages.rhm" open

  "../lib/ordered_drawing.rhm"

  "../components/movement.rhm" open
  "../components/graphics.rhm" open
  "../components/character_state.rhm" open
  "../components/damage.rhm" open
  "../components/item.rhm" open

export:
  CharacterDrawingSystem
  ItemDrawingSystem
  ItemAnimationSystem

use_static

def hurt: Color(200,80,80,255)
def_texture item_texture: "./resources/images/sprites-out.png"

system ItemAnimationSystem(
     sprite :: ItemSprite,
     animation :: ItemSpriteAnimation
  ) with this:

    method update():
      def current_time: raylib.GetTime()
      def delta: current_time - animation.start_time
      sprite.offset_y := math.max(100.0 * delta - 120.0 * delta * delta, 0.)
      when (sprite.offset_y <= 0):
        event.send(RemoveComponent(this, ItemSpriteAnimation))

system ItemDrawingSystem(
    position :: Position,
    sprite :: ItemSprite
  ):

    method draw():
      def item_sprite :: Sprite = sprite.sprite
      def rect :: Rectangle: item_sprite.sprite_rect
      ordered_drawing.queue_draw(
         item_texture,
         math.inexact(position.value.x - 16.),
         math.inexact(position.value.y - 16. - sprite.offset_y),
         32.,
         32.,
         rect.x,
         rect.y,
         rect.width,
         rect.height,
         0.,
         0.,
         raylib.WHITE,
         position.value.y - 32.0
       )



system CharacterDrawingSystem(
    position :: Position,
    anim :: AnimatedSprite,
    orientation :: Orientation
  ) with this:

    method draw():
      def color:
        if this.has_component(ReceivingDamageFrom)
        | hurt.inner()
        | raylib.WHITE
      ordered_drawing.queue_draw(
         anim.texture,
         math.inexact(position.value.x - 32.),
         math.inexact(position.value.y - 32.),
         64.,
         64.,
         math.inexact(64. * anim.frame_counter),
         math.inexact(64. * orientation.facing_direction.ind),
         64.,
         64.,
         0.,
         0.,
         color
       )
