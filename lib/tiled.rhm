#lang rhombus

import:
  rhombus/meta open
  lib("xml/main.rkt") as xml
  lib("racket/main.rkt") as racket

export:
  only_space annot: Object 
  CollisionBox Point Polygon

  Tileset


  ObjectGroup
  Layer
  rename: TiledMap as Map
  load_map
  load_tileset

class Tileset(
  tile_width :: Integral,
  tile_height :: Integral,
  tile_count :: Integral,
  columns :: Integral,
  image :: String,
)

class Object():
  nonfinal
class CollisionBox(
   x :: Real,
   y :: Real,
   width :: Real,
   height :: Real
):
  extends Object

class Point(name :: String, x :: Real, y :: Real):
  extends Object
class Polygon(name :: String, x :: Real, y :: Real, points :: List.of(Pair.of(Real, Real))):
  extends Object

class ObjectGroup(
  name :: String,
  elements :: List.of(Object)
)

class Layer(
   name :: String,
   properties :: Map.of(String, Integral),
   data :: List.of(List.of(Integral))
 )

class TiledMap(
   width :: Integral, height :: Integral,
   tile_width :: Integral, tile_height :: Integral,
   tilesets :: List.of(Pair.of(Integral, Tileset)),
   layers :: List.of(Layer),
   objects :: List.of(ObjectGroup)
 )

fun
| lookup(data, key):
    lookup(data,key,fun (): error((to_string(key) :~ String) ++ " not found"))
| lookup([], id, default :: Function): default()
| lookup([], id, default): default
| lookup([[key, vl], tl, ...], id, default):
    if key == id | vl | lookup([tl,...], id, default)
| lookup([[key, vl, ...], tl, ...], id, default):
    if key == id | [vl, ...] | lookup([tl,...], id, default)

fun read_map_data(fname):
  def data: xml.#{read-xml}(racket.#{open-input-file}(fname))

  xml.#{xml->xexpr}(
    xml.#{eliminate-whitespace}([#'map, #'layer, #'objectgroup, #'object, #'tileset, #'wangsets, #'wangset, #'properties, #'property])(
      xml.#{document-element}(data)
    )
  )

fun read_object(object):
  match object
  | [#'object, meta_data, [#'point, []]]:
      def name: to_string(lookup(meta_data, #'name))
      def x: racket.#{string->number}(lookup(meta_data, #'x))
      def y: racket.#{string->number}(lookup(meta_data, #'y))
      Point(name, x, y)
  | [#'object, meta_data, [#'polygon, data]]:
      def name: to_string(lookup(meta_data, #'name))
      def x: racket.#{string->number}(lookup(meta_data, #'x))
      def y: racket.#{string->number}(lookup(meta_data, #'y))
      def points:
        def elts: racket.#{string-split}(to_string(lookup(data, #'points)), " ")
        for List:
          each elt: elts
          def [px, py]: racket.#{string-split}(elt, ",")
          Pair(racket.#{string->number}(px), racket.#{string->number}(py))
      Polygon(name, x, y, points)
  | [#'object, meta_data]:
      def x: racket.#{string->number}(lookup(meta_data, #'x))
      def y: racket.#{string->number}(lookup(meta_data, #'y))
      def height: racket.#{string->number}(lookup(meta_data, #'height))
      def width: racket.#{string->number}(lookup(meta_data, #'width))
      CollisionBox(x,y,width,height)

fun read_layer_data(data :: String):
  def rows:
    racket.#{string-split}(
      racket.#{string-trim}(data),
      "\n"
    )
  for List:
    each row: rows
    def row_trimmed: racket.#{string-trim}(row)
    def cols: racket.#{string-split}(row_trimmed, ",")
    for List:
      each col: cols
      racket.#{string->number}(col)

fun load_tileset(fname):
  def path: Path(fname)
  def values(tileset_root_dir, _, _): racket.#{split-path}(path)

  def [#'tileset, meta_data, data, ...]: read_map_data(fname)
  def tile_data: [data, ...]

  def tile_width: racket.#{string->number}(lookup(meta_data, #'tilewidth))
  def tile_height: racket.#{string->number}(lookup(meta_data, #'tileheight))
  def tilecount: racket.#{string->number}(lookup(meta_data, #'tilecount))
  def columns: racket.#{string->number}(lookup(meta_data, #'columns))

  def image: lookup(tile_data, #'image)
  def source: to_string(lookup(image, #'source))
  def source_path: racket.#{build-path}(tileset_root_dir, source)
  Tileset(tile_width, tile_height, tilecount, columns, (source_path.string()))

fun read_properties(properties):
  for Map:
    each data: properties
    match data
    | [#'property, fields]:
        def name: to_string(lookup(fields, #'name))
        def value: racket.#{string->number}(lookup(fields, #'value))
        values(name, value)

fun load_map(fname):
  def path: Path(fname)
  def values(map_root_dir, _, _): racket.#{split-path}(path)
  def map_data: read_map_data(path.string())
  def [#'map, meta_data, data, ...]: map_data

  def tile_height: racket.#{string->number}(lookup(meta_data, #'tileheight))
  def tile_width: racket.#{string->number}(lookup(meta_data, #'tilewidth))
  def width: racket.#{string->number}(lookup(meta_data, #'width))
  def height: racket.#{string->number}(lookup(meta_data, #'height))

  def values(tilesets, layers, objects):
    for values(tilesets=[],layers=[],objects=[]):
      each element: [data, ...]
      match element
      | [#'tileset, tileset_data]:
          def source: lookup(tileset_data, #'source)
          def first_gid: racket.#{string->number}(lookup(tileset_data, #'firstgid))
          def source_path: racket.#{build-path}(map_root_dir, source)
          let tilesets: List.cons(Pair(first_gid, load_tileset(source_path.string())), tilesets)
          values(tilesets, layers, objects)
      | [#'layer, meta_data, layer_data, ...]:
          def name: to_string(lookup(meta_data, #'name))
          def property_data: lookup([layer_data, ...], #'properties, #false)
          def properties:
            if property_data
            | def [_, property, ...]: property_data
              read_properties([property, ...])
            | Map{}
          def [encoding, data]: lookup([layer_data, ...], #'data)
          def layer_data: read_layer_data(to_string(data))
          let layers: List.cons(Layer(name, properties, layer_data), layers)
          values(tilesets, layers, objects)
      | [#'objectgroup, meta_data, object, ...]:
          def name: to_string(lookup(meta_data, #'name))
          def collision_objects: [read_object(object), ...]
          let objects: List.cons(ObjectGroup(name, collision_objects), objects)
          values(tilesets, layers, objects)
  TiledMap(
    width, height,
    tile_width, tile_height,
    tilesets.reverse(),
    layers.reverse(),
    objects.reverse()
  )

